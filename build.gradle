buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'

repositories {
	maven {
		// Tesla
		name = "mcmoddev"
		url = "http://maven.mcmoddev.com/"
	}
	maven {
		// JEI
		name = "progwml6"
		url = 'http://dvs1.progwml6.com/files/maven/'
	}
	maven {
		// CraftTweaker
		name = "blamejared"
		url = 'http://maven.blamejared.com/'
	}
	maven {
		// OpenComputers
		name = "cil"
		url = 'http://maven.cil.li/'
	}
	maven {
		// ComputerCraft
		name = "computercraft"
		url = 'http://cc.crzd.me/maven/'
	}
	maven {
		// IC2
		name = "ic2"
		url = 'http://maven.ic2.player.to/'
	}
}

version = "${mod_version}--${mc_version}"
group = "nc"
archivesBaseName = "NuclearCraft"
sourceCompatibility = targetCompatibility = '1.8'
compileJava {
	sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
	version = "${forge_version}"
	runDir = "run"
	replace "@VERSION@", "${mod_version}"
	mappings = "snapshot_20171003"
	makeObfSourceJar = false
}

dependencies {
	// compile against the JEI API but do not include it at runtime
	deobfProvided "mezz.jei:jei_${mc_version}:${jei_version}:api"
	// at runtime, use the full JEI jar
	runtime "mezz.jei:jei_${mc_version}:${jei_version}"
	compile group: "net.industrial-craft", name: "industrialcraft-2", version: "${ic2_version}", classifier: "api"
	compile group: "CraftTweaker2", name: "CraftTweaker2-API", version: "${crafttweaker_version}"
}

processResources {
	exclude '**/Thumbs.db'
	
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}
	
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}
